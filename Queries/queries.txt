# 1

match (author:Author)-[:Writes]->(art:Article)<-[c:Cites]-(:Article)
with author, art, count(c) as numbOfCitations
order by author, numbOfCitations desc
with author, collect(numbOfCitations) as artCit
return author.name, artCit, size([pos in range(1,size(artCit)) where artCit[pos-1] >= pos]) as h_index

# 2

match (conf:Conference)-->(:Edition)-->(art:Article)<-[c:Cites]-()
with conf, art, count(c) as citations
order by conf, citations desc
return conf, collect({article:art.title, cit:citations})[0..3] as listCit

# 3

match (author:Author)-[:Writes]->(art:Article)<-[:Contains]-(e:Edition)<-[:Has_an]-(conf:Conference)
with conf.name as conf, author.name as Author, collect (distinct e.number) as numPub 
order by conf
where size(numPub)>=4
return conf as Conference, Author, numPub as Edition_Number
